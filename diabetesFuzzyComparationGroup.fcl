/*
	Calcula se a pessoa tem risco de diabetes baseando-se em idade, triglicerideos, tempoEvolutivo, IMC,
	circunferenciaAbdominal, renda e escolaridade
*/

FUNCTION_BLOCK diabetesFuzzy	// Bloco de definição

VAR_INPUT				// Definição das variáveis de entrada
	idade: REAL;
	triglicerideos: REAL;
	tempoEvolutivo: REAL;
	IMC: REAL;
	circunferenciaAbdominal: REAL;
	renda: REAL;
	escolaridade: REAL;
END_VAR

VAR_OUTPUT				// Definição da variável de saída
	diabetes : REAL;
END_VAR

FUZZIFY idade			// Fuzzyficacao da variavel de entrada 'idade': {'baixa' , 'media' , 'alta'}
	TERM baixa := (3, 0) (22, 1) (41, 0); 
	TERM media := (22, 0) (41, 1) (59.9, 0);
	TERM alta := (41.0, 0) (60, 1) (79, 0);
END_FUZZIFY

FUZZIFY triglicerideos			// Fuzzyficacao da variavel de entrada 'triglicerideos': {'baixo' , 'medio' , 'alto'}
	TERM baixo := (-135, 0) (45, 1) (225, 0);
	TERM medio := (45, 0) (225, 1) (405, 0);
	TERM alto := (225, 0) (405, 1) (585, 0);
END_FUZZIFY

FUZZIFY tempoEvolutivo			// Fuzzyficacao da variavel de entrada 'tempoEvolutivo': {'baixo' , 'medio' , 'alto'}
	TERM baixo := (-11.63, 0) (0.25, 1) (12.1, 0);
	TERM medio :=  (0.27, 0) (12.1, 1) (24,0);
	TERM alto :=  (12.1, 0) (24, 1) (35.9, 0); 
END_FUZZIFY

FUZZIFY IMC		// Fuzzyficacao da variavel de entrada 'IMC': {'baixo' , 'medio' , 'alto'}
	TERM baixo := (2.2, 0) (21.7, 1) (41.2, 0);
	TERM medio := (21.7, 0) (41.2, 1) (60.7, 0);
	TERM alto := (41.2, 0) (60.7, 1) (80.2, 0);
END_FUZZIFY

FUZZIFY circunferenciaAbdominal		// Fuzzyficacao da variavel de entrada 'circunferenciaAbdominal': {'baixa' , 'media' , 'alta'}
	TERM baixa := (52.8, 0) (81,1) (109.2, 0);
	TERM media := (81, 0) (109.2, 1) (137.5, 0);
	TERM alta := (109.3, 0) (137.5, 1) (165.8, 0);
END_FUZZIFY

FUZZIFY renda		// Fuzzyficacao da variavel de entrada 'renda': {'baixa' , 'media' , 'alta'}
	TERM baixa := (-500, 0) (-0.000092, 1) (500, 0);
	TERM media := (0.000084, 0) (500, 1) (1000,0); 
	TERM alta := (500, 0) (1000, 1) (1500, 0);
END_FUZZIFY

FUZZIFY escolaridade		// Fuzzyficacao da variavel de entrada 'escolaridade': {'baixa' , 'media' , 'alta'}
	TERM baixa := (-7.5, 0) (-0.000026, 1) (7.5, 0);
	TERM media := (0.0064, 0) (7.5, 1) (14.9, 0);
	TERM alta := (7.5, 0) (15, 1) (22.5, 0);
END_FUZZIFY

DEFUZZIFY diabetes			// Dezfuzzyficacao da variavel de saida 'diabetes' : {'diminuiuMuito', 'diminuiu', 'estavel', 'aumentou'}
	TERM diminuiuMuito :=  (-4, 0) (-2.47, 1) (-1.08, 0);
	TERM diminuiu :=  (-2.014, 0) (-1.15, 1) (-0.2857, 0);
	TERM estavel :=  (-0.7452, 0) (0.00000000000000001388, 1) (0.745, 0);
	TERM aumentou :=  (0.194, 0) (1.881, 1) (3.24, 0);
	METHOD : COG;		// Use 'Center Of Gravity' defuzzification method
	DEFAULT := 2;		// Default value is 0 (if no rule activates defuzzifier)
END_DEFUZZIFY

RULEBLOCK No1
	AND : MIN;			// Use 'min' for 'and' (also implicit use 'max' for 'or' to fulfill DeMorgan's Law)
	ACT : MIN;			// Use 'min' activation method
	ACCU : MAX;			// Use 'max' accumulation method

	RULE 1: IF renda IS baixa AND idade IS baixa THEN diabetes IS aumentou;
	RULE 2: IF renda IS baixa AND idade IS media AND circunferenciaAbdominal IS baixa THEN diabetes IS estavel;
	RULE 3: IF renda IS baixa AND idade IS media AND circunferenciaAbdominal IS media THEN diabetes IS aumentou;
	RULE 4: IF renda IS baixa AND idade IS media AND circunferenciaAbdominal IS alta THEN diabetes IS aumentou;
	RULE 5: IF renda IS baixa AND idade IS alta THEN diabetes IS diminuiu;
	RULE 6: IF renda IS media AND escolaridade IS baixa THEN diabetes IS estavel;
	RULE 7: IF renda IS media AND escolaridade IS media THEN diabetes IS diminuiu;
	RULE 8: IF renda IS media AND escolaridade IS alta AND triglicerideos is baixo THEN diabetes IS aumentou;
	RULE 9: IF renda IS media AND escolaridade IS alta AND triglicerideos is medio THEN diabetes IS aumentou;
	RULE 10: IF renda IS media AND escolaridade IS alta AND triglicerideos is alto THEN diabetes IS diminuiuMuito;
	RULE 11: IF renda IS alta THEN diabetes IS diminuiu;

END_RULEBLOCK

END_FUNCTION_BLOCK

