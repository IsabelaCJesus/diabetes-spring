/*
	Calcula se a pessoa tem risco de diabetes baseando-se em idade, triglicerideos, tempoEvolutivo, IMC,
	circunferenciaAbdominal, renda e escolaridade
*/

FUNCTION_BLOCK diabetesFuzzy	// Bloco de definição

VAR_INPUT				// Definição das variáveis de entrada
	idade: REAL;
	triglicerideos: REAL;
	tempoEvolutivo: REAL;
	IMC: REAL;
	circunferenciaAbdominal: REAL;
	renda: REAL;
	escolaridade: REAL;
END_VAR

VAR_OUTPUT				// Definição da variável de saída
	diabetes : REAL;
END_VAR

FUZZIFY idade			// Fuzzyficacao da variavel de entrada 'idade': {'baixa' , 'media' , 'alta'}
	TERM baixa := (19.5, 0) (33, 1) (46.5, 0); 
	TERM media := (33, 0) (46.5, 1) (60, 0);
	TERM alta := (46.5, 0) (60, 1) (73.5, 0);
END_FUZZIFY

FUZZIFY triglicerideos			// Fuzzyficacao da variavel de entrada 'triglicerideos': {'baixo' , 'medio' , 'alto'}
	TERM baixo := (-174, 0) (47, 1) (268.5, 0);
	TERM medio := (47, 0) (268.5, 1) (490, 0);
	TERM alto := (268.5, 0) (490, 1) (711, 0);
END_FUZZIFY

FUZZIFY tempoEvolutivo			// Fuzzyficacao da variavel de entrada 'tempoEvolutivo': {'baixo' , 'medio' , 'alto'}
	TERM baixo := (-15.2, 0) (0.20, 1) (15.6, 0);
	TERM medio :=  (0.2, 0) (15.6, 1) (31,0);
	TERM alto :=  (15.6, 0) (31, 1) (46.4, 0); 
END_FUZZIFY

FUZZIFY IMC		// Fuzzyficacao da variavel de entrada 'IMC': {'baixo' , 'medio' , 'alto'}
	TERM baixo := (3.7, 0) (16.06, 1) (28.4, 0);
	TERM medio := (16.1, 0) (28.41, 1) (40.8, 0);
	TERM alto := (28.1, 0) (40.8, 1) (53.1, 0);
END_FUZZIFY

FUZZIFY circunferenciaAbdominal		// Fuzzyficacao da variavel de entrada 'circunferenciaAbdominal': {'baixa' , 'media' , 'alta'}
	TERM baixa := (45, 0) (72,1) (99, 0);
	TERM media := (72, 0) (99, 1) (126, 0);
	TERM alta := (99, 0) (126, 1) (153, 0);
END_FUZZIFY

FUZZIFY renda		// Fuzzyficacao da variavel de entrada 'renda': {'baixa' , 'media' , 'alta'}
	TERM baixa := (-400, 0) (0.0000448, 1) (400, 0);
	TERM media := (0.000061, 0) (400, 1) (800,0); 
	TERM alta := (400, 0) (800, 1) (1200, 0);
END_FUZZIFY

FUZZIFY escolaridade		// Fuzzyficacao da variavel de entrada 'escolaridade': {'baixa' , 'media' , 'alta'}
	TERM baixa := (-7.5, 0) (-0.0011, 1) (7.5, 0);
	TERM media := (0.0011, 0) (7.5, 1) (15, 0);
	TERM alta := (7.5, 0) (15, 1) (22.5, 0);
END_FUZZIFY

DEFUZZIFY diabetes			// Dezfuzzyficacao da variavel de saida 'diabetes' : {'diminuiuMuito', 'diminuiu', 'estavel', 'aumentou'}
	TERM diminuiuMuito :=  (-6.69, 0) (-3.84, 1) (-1.87, 0);
	TERM diminuiu :=  (-3.33, 0) (-1.959, 1) (-0.354, 0);
	TERM estavel :=  (-0.836, 0) (0.03296, 1) (0.9019, 0);
	TERM aumentou :=  (0.3609, 0) (1.71, 1) (2.56, 0);
	METHOD : COG;		// Use 'Center Of Gravity' defuzzification method
	DEFAULT := 2;		// Default value is 0 (if no rule activates defuzzifier)
END_DEFUZZIFY

RULEBLOCK No1
	AND : MIN;			// Use 'min' for 'and' (also implicit use 'max' for 'or' to fulfill DeMorgan's Law)
	ACT : MIN;			// Use 'min' activation method
	ACCU : MAX;			// Use 'max' accumulation method

	RULE 1: IF escolaridade IS baixa AND circunferenciaAbdominal IS baixa THEN diabetes IS diminuiuMuito;
	RULE 2: IF escolaridade IS baixa AND circunferenciaAbdominal IS media AND IMC IS baixo THEN diabetes IS diminuiu;
	RULE 3: IF escolaridade IS baixa AND circunferenciaAbdominal IS media AND IMC IS medio THEN diabetes IS diminuiu;
	RULE 4: IF escolaridade IS baixa AND circunferenciaAbdominal IS media AND IMC IS alto THEN diabetes IS aumentou;
	RULE 5: IF escolaridade IS baixa AND circunferenciaAbdominal IS alta THEN diabetes IS estavel;
	RULE 6: IF escolaridade IS media AND renda IS baixa THEN diabetes IS aumentou;
	RULE 7: IF escolaridade IS media AND renda IS media THEN diabetes IS diminuiu;
	RULE 8: IF escolaridade IS media AND renda IS alta AND idade IS baixa THEN diabetes IS aumentou;
	RULE 9: IF escolaridade IS media AND renda IS alta AND idade IS media THEN diabetes IS aumentou;
	RULE 10: IF escolaridade IS media AND renda IS alta AND idade IS alta THEN diabetes IS estavel;
	RULE 11: IF escolaridade IS alta THEN diabetes IS diminuiu;

END_RULEBLOCK

END_FUNCTION_BLOCK

